
class TPuzzle	{
	init	{}
	tinit(string sbkg, string sels, int row, int col, int dx, int dy)	{
		new int iCols = col;
		new int iRows = row;
		.newimg("imgbkg", sbkg, 0);
		
		new gmimgvec grel;
		new gmimgvec grend;
		"grel" ..< sels;
		grel.hash;
		grel.each( func { (@id) .setz(10+id); } );
		
		new int ibadajl = 25;
		new string sactpuz;
		new int iblitopacity = 192;
		new int PuzzleState = 0;
		
		new DelayMover cmov;
		new SimpleCounter cnt(200);
		
		new int iX = dx;
		new int iY = dy;
		grel.move( iX, iY );
	}
	setpuzstate(int istate)	{
		PuzzleState=istate;
		if( PuzzleState==1)	{
			new gmimgvec grblit;
			@s = _;
			"grblit" ..< s;
			grblit.hide;
		}
	}
	cmov_GET	{
		if( igmstate!=1 ) return;
		if( grel.isincut( mouse.getpos, true, true ) )	{
			string s = grel.getsfound;
			<s>.setz(900);
			.mssetobj( s );
			.tpuzzle_get;
		}
	}
	bool cwithin(string s)	{
		int i;
		if( .gettype!="anima" )	{
			for( i=0; i < .size; i++)	{if( <.get(i)>.cwithin( s ) )	return true;}
			return false;
		} else {
			if( <s>.gettype=="anima" )	{
				int id = .framenr;
				int im = id%iCols;
				A;
				if( im>0 )			id-1;
				if( im<iCols-1 )	id+1;
				im = id/iCols;
				if( im>0 )			id-iCols;
				if( im<iRows-1)	id+iCols;
				return <s>.framenr->in;
			} else {
				for( i=0; i< <s>.size; i++)		{if( .cwithin( <s>.get(i) ) )	return true;}
				return false;
			}
		}
	}
	cblit(string s)	{
		if( <s>.gettype=="anima" )	{
			if( PuzzleState==1 )	{
				<s>.setpos(iX, iY);
				<s>.hide;
				s = grblit.get(<s>.framenr);
			}
			<s> {
				.setz(1);
				.show;
				
				.setpos(iX,iY);
				int x = .getpx;
				int y = .getpy;
				.anaddfilter;
				.setopacity(iblitopacity);
				.setpos(x,y);
				imgbkg.blit( this );
				.unlink;
				.setpos(iX,iY);
				.hide;
			};
		} else	for( int i=0; i< <s>.size; i++ )		.cblit( <s>.get(i) );
	}
	cmov_PUT	{	<GAME>.cput;	}
	cput	{
		.tpuzzle_put;
		string s = cmov.getmover;
		<s>.setz(100+cnt.next);
		cmov.free;
		grel {
			.sortimgs;
			.each( func { (int i) .setz(10+i); } );
		};
		string s1 = s, string s2;
		sactpuz = s;
		while(<s>.gettype != "anima")	s=<s>.get(0);
		if( ibadajl.length( <s>.lodx+iX-<s>.getpx, <s>.lody+iY-<s>.getpy ) < ibadajl )	{
			sactpuz = null;
			.cblit(s1);
			grel.remove(s1);
			grend.add(s1);
			.tpuzzle_blit;
			if( grel.empty )	{
				grend.show;
				.tpuzzle_end;
			}
		} else {
			grel.remove(s1);
			for( int i=0; i<grel.size; i++)	{
				s = grel.get(i);
				if( cllen.iposlen(s,s1)<0 && <s1>.cwithin(s) )	{	// nie laczyc
				//if( cllen.iposlen(s,s1)<ibadajl && <s1>.cwithin(s) )	{
					|int x, int y| = <s>.getpos;
					if( <s>.gettype=="anima")	{
						.mwyjmij(s);
						if( <s1>.gettype=="anima" )	{
							s2 = "gr" + s;
							new gmimgvec <s2>;
							<s2>.add(s);
							<s2>.add(s1);
						} else {
							<s1>.add(s);
							s2 = s1;
						}
						grel.remove(s);
						grel.add(s2);
					} else {
						if( <s1>.gettype=="anima" )	{
							<s>.add(s1);
						} else {
							<s>.addgr(s1);
						}
						s2 = s;
					}
					<s2>.setpos(x,y);
					<s2>.setz( <s1>.getz );
					grel.sortimgs;
					sactpuz = s2;
					return;
				}
			}
			grel.add(s1);
		}
	}
	virtual tpuzzle_end {}
	virtual tpuzzle_get {}
	virtual tpuzzle_blit {}
	virtual tpuzzle_put {}
}

game Puzzle : Kajuta, TPuzzle	{
	init	{
		Kajuta::init("puzzle");
		
		.titsounds = (A, "lektor1", "Na górze ekranu s± fragmenty po³±czneñ. Uk³adaj je w odpiednich miejscach tak aby electricity, oxygen, fuel po³aczy³y siê odpowiednimi zegarami po przeciwnej stronie ekranu.", func { .unlockall; .stdret; }
			);
		
		.sfxs = (A, "getpuz 60", "putpuz 60", "ok 60", "puzzleok 50", "putpuz2 60");
		
		new int iPuzRow = 4;
		new int iPuzCol = 5;
		new bool bPlayMusic = true;
		.tinit("bgpuzzle.png", "puz4_5.pyz", iPuzRow, iPuzCol, 0, 0);
		
		.imgs = (A, "left.pyz 200", "right.pyz 200", "maski.pyzG 199", "finisz.pyzH 20");
		
		anleft.Button_std;
		anright.Button_std;
		but1.add("anleft");
		but1.add("anright");
		new int strzalkax = 5;
		
		new int rewindspeed = 10;
		.cycle = ("timleft", func {
			if( igmstate==1 && grpes.size )	{
				grpes.move( rewindspeed, 0 );
				@s = grpes.first;
				if( <s>.getpx > anleft.getex + strzalkax )
					grpes.move( (anleft.getex+strzalkax)-<s>.getpx, 0 );
				.play;
			}
			} );
		.cycle = ("timright", func {
			if( igmstate==1 && grpes.size )	{
				grpes.move( -rewindspeed, 0 );
				@s = grpes.last;
				if( <s>.getex < anright.getpx - strzalkax )
					grpes.move( (anright.getpx-strzalkax)-<s>.getex, 0 );
				.play;
			}
			} );
		
		new Rect rec2(6,54,1012,690);
		new gmimgvec grpes;
		for( int i=0; i<grel.size; i++ )
			.mwstaw(grel.get(i));
		
		/*.cbutexit;
		anbutexit.move(-15,-15);*/
		
		.timer = ("timwatch", 2000, "mend" );
		
		//.cbutmusic;
		
		if( igmdebug )	.mbutnext;
		.mskipminigame;
		.match3but;
		if( Trudnosc<2) anmatch3but.move(0, iResY - anmatch3but.getey - 120 );
		
		if( bPlayMusic )	{
			//mus.playstr("wizards.ogg -v 50 -l");
		}
		ccs.enter( func {
			.txtplay("lektor1");
			} );
	}
	mend	{
		if( AdventureMode )
			.mprzejdz("HasloA");
		else .mprzejdz("MainMenu");
	}
	match3_won	{ .mend;	}
	skip_minigame	{	.mend;	}
	play_music	{	mus.playstr("wizards.ogg -v 50 -l");	}
	stop_music	{	mus.fadeout(1000);	}
	mwstaw(string s)	{
		int dy = 5;
		int xpos;
		if( grpes.size )	{
			xpos = <grpes.last>.getex;
		} else {
			xpos = anleft.getex;
		}
		<s>.ansetbpos( xpos + strzalkax, <grmaski.first>.getpy + 10 );
		<s>.clip( anleft.getex + strzalkax, 0, anright.getpx-strzalkax, iResY );
		grpes.add(s);
	}
	mwyjmij(string s)	{
		int id = grpes.find(s);
		if( id<0 ) return;
		<s>.clip(0,0,iResX,iResY);
		grpes.remove(s);
		for( int i=id; i < grpes.size; i++ )	{
			<grpes.get(i)>.move( -<s>.getw-strzalkax, 0 );
		}
	}
	clbuts_help_butmoveon	{
		//if( igmstate!=2)
			//anpodpo.show;
	}
	clbuts_help_butmoveoff	{
		//if( igmstate!=2)
			//anpodpo.hide;
	}
	tpuzzle_get	{
		fxgetpuz.play;
		.mwyjmij(grel.getsfound);
	}
	tpuzzle_put	{	fxputpuz.play;	}
	tpuzzle_blit	{	fxputpuz2.play;	}
	tpuzzle_end	{
		fxok.play;
		grend.hide;
		igmstate=2;
		
		anfinisz.play(0);
		timwatch.play;
		fxpuzzleok.play;
	}
	anbutnext_click	{
		.tpuzzle_end;
	}
	cmov_PUT	{
		<GAME>.cput;
		if( sactpuz!=null )	{
			if( <sactpuz>.gettype=="anima" && <sactpuz>.getpy < anleft.getcy )	{
				.mwstaw(sactpuz);
			} else {
				rec2.fit(sactpuz);
			}
		}
		
	}
	tit_lclick	{
		|int x, int y| = mouse.getpos;
		if( igmstate==1 )	{
			if( anleft.isin(x,y,1,1) ) timleft.play;
			else if (anright.isin(x,y,1,1) ) timright.play;
		}
	}
	mouse_lrel	{
		if( igmstate==1 )	{
			if( timleft.isplaying ) timleft.stop(false);
			if( timright.isplaying ) timright.stop(false);
		}
	}
	mouse_rclick	{
		//if( igmdebug ) .tpuzzle_end;
		if( igmdebug ) grpes.print;
	}
}

