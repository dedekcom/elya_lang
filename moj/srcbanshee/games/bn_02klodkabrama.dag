
game mg02Klodkabrama : Banshee	{
	init	{
		Banshee::init("mg02_klodkabrama");
		.imgs = (A, "bkg.jpg", "lew.pyz 30", "oczy.pyzH 40",
			"box.pyzH 5", "els.pyzGH 10", "forklucz.pyzH 15", "kolatka.pyzH 40", "klucz.pyzH 20","final_glow.pyzH 21",
			"popexit.pyz 50");
		
		.butpopexit;
		.askopis("mg02_klodkabrama"); 
		//.addtopck("kolatka"); //testy
		//.addtopck("kluczschodek");//testy
		anaskopis.ansetbpos( anpopexit.getcx + anaskopis.getw, anpopexit.getcy - anaskopis.geth/2);
		anlew {
			.Button_isinalpha;
			butgame.add(this);
		};
		
		imgbkg {
			.addmethod("isbutin", func { (@x, @y)
				!anbox.isin(x,y,0,0);
				} );
			.addmethod("butclick", func {
				if( !.advstdstate ) return;
				.mend;
				} );
			butgame.add(this);
		};     
    
		if( !clsave.bis("e1_lew_losuj") )	{
			clsave.bset("e1_lew_losuj");
			for( int i=0; i<grels.size; i++)	{
				@s = grels.get(i);
				<s>.setframe(i, 1 + (<s>.nofframes(-1)-1)->rand );
				clsave.set("e1_lew_els_"+i, <s>.framenr );
			}
		} else {
			for( int i=0; i<grels.size; i++)	{
				<grels.get(i)>.setframe( i , clsave.get("e1_lew_els_"+i) );
			}
		}
		
		.sfxs = (A, "odklucza2", "monster1", "bgrwiatrN 10", "bgrlas2N 40", "kolatka", "lockdoor1", "ceglaput", "klucz", "przekrecaklucz");
		new bool bkoniecmech;
		.mcheckels;
		
		
		if (clsave.bis("e1_lew_mech") && !bkoniecmech)
		{
			anaskopis.show;
			guihint.switchtoskip;  
		}
		else
			anaskopis.hide;	
		
		anklucz.addmethod("onfinish", "mend");
			
		ankolatka {
			.Button_isin;
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate || clsave.bis("e1_lew_mech")) return;
				sgmstate = "lewopen";
				clsave.bset("e1_lew_mech");
				.play(0);
				} );
			.addmethod("onfinish", func {
				.hide;
				.mlewmech;
				anlew.playsmooth("open");
				anaskopis.show;
				guihint.switchtoskip;  
				fxlockdoor1.play;
				} );
			butgame.add(this);
		};
		anlew {
			.addmethod("isbutin", func { (int x, int y)
				.isin(x,y,1,1) && (.actionname=="normal" || (.actionname=="sleep" && !ankolatka.isvisible));
				} );
			.addmethod("butmoveon", func {
  				 
  				  advmouse.set("use");
  				   
			} ); 	
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate ) return;
				if( .actionname=="normal" )	{
					sgmstate = "angrylew";
					.playsmooth("angry");
					fxmonster1.play;
					anoczy.play("angry");
					.cbplay("snd_lew"); 
				}
				} );
			.addmethod("onfinish", func {
				.setsmooth(0);
				if (.actionname=="angry")	{
					.play("angry2");
				} else if (.actionname=="angry2")	{
					.playsmooth("angry3");
				} else if (.actionname=="angry3")	{
					.advsetstdstate;
					.play("normal");
					anoczy.play("normal");
				} else if ( .actionname=="open" )	{
					.advsetstdstate;
					.setframe("opened", 0);
				} else if (.actionname=="sleep")	{
					.advsetstdstate;
				}
				} );
			butgame.add(this);
		};
		grels.eval( func {
			.addmethod("isbutin", func { (int x, int y)
				!bkoniecmech && .isin(x,y,1,1);
				} );
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate || bkoniecmech ) return;
				.ansetnextfr;
				clsave.set("e1_lew_els_" + .actionnr(-1), .framenr);
				fxklucz.play;
				.mcheckels;
				if( bkoniecmech )	{
					fxodklucza2.play;
					anfinal_glow.anfadein(1000,func{anfinal_glow.anfadeout(1500,null);});
				}
				} );
			butgame.add(this);
			} );
		
		anforklucz	{
			.addmethod("isbutin", func { (int x, int y)
				 bkoniecmech && .isin(x,y,1,0);
				} );
			.addmethod("butmoveon", func {
  				 
  				  advmouse.set("use");
  				   
			} ); 	
			//.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate || (guimov.moving && guimov.getmover=="anitem_kluczschodek") ) return;
					.cbplay("snd_klucz");
			});
			 
			butgame.add(this);
		};
		
		//lew: normal angry sleep open opened
		if( clsave.bis("e1_lew_mech") )	{
			anlew.setframe("opened",0);
			.mlewmech;
		} else if ( clsave.bis("e1_lew_kolatka") )	{
			anlew.setframe("sleep", 0);
			ankolatka.show;
		} else {
			anoczy.play(0);
		}
		 
		/*if( clsave.bis("e1_lew_mech") )	{
			.stdstart;
		} else
			.advsetstdstate;*/
		 
		.newsndfree("snd_lew", "kinnat_c1d1_3d");
		 snd_lew.addmethod("onfinish", "advsetstdstate");  

		.newsndfree("snd_klucz", "kinnat_c1d1_3e");
		 snd_klucz.addmethod("onfinish", "advsetstdstate"); 	
		 
		.stdstart; 

	}
	mlewmech	{
		anbox.show;
		grels.show;
		anforklucz.show;
	}
	mcheckels	{
		for( int i=0 ; i<grels.size; i++) {
			if( !<grels.get(i)>.framename->contains("ok") ) {
				bkoniecmech = false;
				return;
			}
		}
		bkoniecmech = true;
	}
	USE_kolatka	{
		if( anlew.isin(mouse.getpos,1,1) )	{
			.removefrompck("kolatka");
			anlew.play("sleep");
			anoczy.anhide;
			anoczy.anfadeout(800,null);
			ankolatka.anfadein(800,null);
			fxceglaput.play;
			clsave.bset("e1_lew_kolatka");
			 
		} else {
			.advsetstdstate;
			.item_gotopck;
		}
	}
	USE_kluczschodek	{
		if( bkoniecmech && anforklucz.isin(mouse.getpos,1,0) )	{
			.removefrompck("kluczschodek");
			sgmstate = "kluczotwiera";
			anforklucz.butdisable;
			advmouse.setstd;
			clsave.bset("e1_lew_otworzony");
			fxodklucza2.play;
			fxprzekrecaklucz.play;
			anklucz.play(0);
		} else {
			.advsetstdstate;
			.item_gotopck;
		}
	}
	mend	{
		.stdexitdefadv("ad02_brama", "gamead02_brama0");
	}
	HINT_CLICK	{
		if( !clsave.bis("taken_kolatka") )	{
			.showhint("anpopexit");
		} else if ( !clsave.bis("e1_lew_kolatka") )	{
			.showhint("anlew");
			.showhintitem("kolatka");
		} else if ( !clsave.bis("e1_lew_mech") )	{
			.showhint("ankolatka");
		} else if( !bkoniecmech )	{
			for( int i=0; i<grels.size; i++ )	{
				if( !<grels.get(i)>.framename->contains("ok") )	{
					.showhint(grels.get(i));
					return;
				}
			}
		} else if( !clsave.bis("taken_kluczschodek") )	{
			.showhint("anpopexit");
		} else if ( !clsave.bis("e1_lew_otworzony") )	{
			.showhint("anforklucz");
			.showhintitem("kluczschodek");
		} else
			.showhint("anpopexit");
	}
	
	SKIP_CLICK {
		for( int i=0 ; i<grels.size; i++) {
			  <grels.get(i)>.setframe(-1,0);
			 clsave.set("e1_lew_els_"+i, 0);
		}
		bkoniecmech = true;
		anaskopis.anfadeout(500,null);
		guihint.switchtohint; 
	}  
	
}


