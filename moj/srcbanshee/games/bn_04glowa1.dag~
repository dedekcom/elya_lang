
game mg04_glowa1 :Banshee {
	init{
    Banshee::init("mg04_glowa1");
      .newsndfree("snd_brak_krysztalow", "kinnat_28a");
      snd_brak_krysztalow.addmethod("onfinish", "advsetstdstate");  
      
      .imgs = (A,"bkg.jpg ", "bkg.pyz 0", "wnetrze_czaszki.pyz 1", "baza.pyz 2",   "grom_normal_g.pyzH 4", "oko_l.pyz 5", "oko_p.pyz 6",  "czaszka_l.pyz 7", "czaszka_p.pyz 7", "ramka.pyz 8" , "grom_normal_l.pyzH 10", "grom_normal_p.pyzH 11", "grom_normal_d.pyzH 12", "krysztal.pyzH 13","krysztal_mask.pyzH 14","glowa1_element.pyzH 15", "popexit.pyz 50" );
      
      .sfxs = (A, "bgrmorze3N 70", "szczeka", "openhead", "ceglamove2", "podswietl");
      
      imgbkg {
  		.addmethod("isbutin", func { (@x, @y)
  			!anbkg.isin(x,y,0,0);
  			} );
  		.addmethod("butclick", func {
  			if( !.advstdstate ) return;
  			 .mend;
  			} );
    		butgame.add(this);
  	 };       
          
     	.butpopexit;
      .setasitemtopck("anglowa1_element");
        
      new vector crystal_states;
      crystal_states.type("string");
      crystal_states.beginadd("begin", "redblue", "empty", "blue", "red", "empty", "blue", "blue", "blue");
      
      new vector crystal_states_2;
      crystal_states_2.type("string");
      crystal_states_2.beginadd("begin", "red", "empty", "empty", "blue", "red", "red", "red"); 
     
      new int click_counter = -1;
     .init_crystals(); 
      
      if (clsave.bis("mg04_glowa1_saved")) 
       .load_ring();
      
    
    //flagi 
    
      
      // do testow
     /* if (!clsave.bis("bn_04_glowa_1_krysztaly_dodane"))
      {
        .addtopck("krysztal_blue_1");
        .addtopck("krysztal_red_1");
        .addtopck("krysztal_red_2"); 
        .addtopck("krysztal_blue_2");
        clsave.bset("bn_04_glowa_1_krysztaly_dodane"); 
      }   */    
         
      if (clsave.bis("bn_04_glowa_1_lewe_oko_zamkniete"))
          anoko_l.hide();
      if (clsave.bis("bn_04_glowa_1_prawe_oko_zamkniete"))
          anoko_p.hide();
      if(clsave.bis("bn_04_glowa_1_czaszka_otwarta")&& !clsave.bis("taken_glowa1_element"))
      {
          angrom_normal_g.show(); 
          angrom_normal_d.show();
          anczaszka_l.show();
          anczaszka_l.setframe("opened", 0); 
          anczaszka_p.show();
          anczaszka_p.setframe("opened", 0);
          anglowa1_element.anfadein(500,null);
      }
      else
      {
          angrom_normal_g.hide(); 
          angrom_normal_d.hide();
      }
    
    
    // buttony 
    
    anoko_l {
			.Button_isin;
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
		 	if( !.advstdstate  ) return;
		 	    if (clsave.bis("bn_04_glowa_1_lewe_oko_zamkniete")) return;
          
          .nokrystal_warning; 
          if (!clsave.bis("bn_04_glowa_1_kregi_rozwiazane"))
          { 
		fxceglamove2.play;
            .rotate_circle_1();  
          }
          else
          {
		fxopenhead.play;
            anoko_l.play("push");
          }
              
			} );
      .addmethod("onfinish", func {
				.hide;
			  clsave.bset("bn_04_glowa_1_lewe_oko_zamkniete");
        if (clsave.bis("bn_04_glowa_1_prawe_oko_zamkniete") && !clsave.bis("bn_04_glowa_1_czaszka_otwarta")) 
        { 
            .open_skull();
        }        
			 } );
			 
			butgame.add(this);
		};
    
    anoko_p {
			.Button_isin;
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
		 	if( !.advstdstate ) return;
		 	    if (clsave.bis("bn_04_glowa_1_prawe_oko_zamkniete")) return;
          
          .nokrystal_warning; 
          if (!clsave.bis("bn_04_glowa_1_kregi_rozwiazane"))
          { 
		fxceglamove2.play;
            .rotate_circle_2();  
          }
          else
          {
		fxopenhead.play;
            anoko_p.play("push");
          }
      } );
      .addmethod("onfinish", func {
  			.hide; 
  		  clsave.bset("bn_04_glowa_1_prawe_oko_zamkniete"); 
        if (clsave.bis("bn_04_glowa_1_lewe_oko_zamkniete") && !clsave.bis("bn_04_glowa_1_czaszka_otwarta")) 
        { 
            .open_skull();
        }
		  } );
		 
		  butgame.add(this);
		}; 
    
     
    .stdstart;
	 
    
    
  } // koniec init
  
  nokrystal_warning()
  {
    click_counter++;
    if (.find_empty_slot > 0 && (click_counter == 0 || click_counter%6 == 0) && .advstdstate)
    {
      .cbplay("snd_brak_krysztalow");    
    }
  }

  //krysztaly  
  init_crystals{  
    .init_crystal("ankrysztal_0", 665, 278);
    .init_crystal("ankrysztal_1", 636, 348);
    .init_crystal("ankrysztal_2", 571, 376);
    .init_crystal("ankrysztal_3", 500, 347);
    .init_crystal("ankrysztal_4", 473, 279);
    .init_crystal("ankrysztal_5", 502, 212);
    .init_crystal("ankrysztal_6", 569, 184);
    .init_crystal("ankrysztal_7", 637, 210);
    
    .init_crystal("ankrysztal_8", 694, 212);
    .init_crystal("ankrysztal_9", 761, 186);
    .init_crystal("ankrysztal_10", 829, 212);
    .init_crystal("ankrysztal_11", 857, 282);
    .init_crystal("ankrysztal_12", 830, 351);
    .init_crystal("ankrysztal_13", 761, 377);
    .init_crystal("ankrysztal_14",695, 351);
    
    .update_crystals();
  }
  
  update_crystals(){
    for( int i=0; i<crystal_states.size; i++)	{ 
      string s = crystal_states.get(i);  
      <"ankrysztal_"+i>.play(s);
    } 
    for( int i=0; i<crystal_states_2.size; i++)	{ 
      string s = crystal_states_2.get(i);  
      int index = i + 8;
      <"ankrysztal_"+index>.play(s);
    }
     .check_rings();     
  }
  
  init_crystal(string name, int x, int y){
    .copyanima("ankrysztal", name);
    .copyanima("ankrysztal_mask", name+"_mask");
    <name>.setpos(x,y);
    <name>.play("empty");
    <name+"_mask">.setpos(x + 8,y +5);
	<name+"_mask">.vars2(A,"name", name); 
    <name+"_mask">{
		.Button_isinvis;  
		.addmethod("butmoveon", func {
		if( !.advstdstate) return;
			if(<.name>.actionnr(-1)== 3)
				advmouse.set("use"); 
			else
			  	advmouse.setstd;         
		} );
		.addmethod("butmoveoff", func {
			if( !.advstdstate ) return;
			  advmouse.setstd;
			} ); 
		butgame.add(this); 
	};
  }
   
  // koniec krysztalow 
  
  // obracanie 
  rotate_circle_1(){
      string last = crystal_states.get(crystal_states.size-1);
      for(int i=crystal_states.size-1; i > 0; i--)	{ 
          crystal_states.set(i,crystal_states.get(i-1));  
      }
       crystal_states.set(0,last); 
      .update_crystals();
             
  } 
   rotate_circle_2(){
      string first = crystal_states.get(0);
      string last = crystal_states_2.get(crystal_states_2.size-1);
       
      for(int i=crystal_states_2.size-1; i > 0; i--)	{ 
          crystal_states_2.set(i,crystal_states_2.get(i -1));  
      }
      crystal_states.set(0,last);
      crystal_states_2.set(0,first);
       
      .update_crystals(); 
           
  } 
  
  check_rings(){
    if (clsave.bis("bn_04_glowa_1_kregi_rozwiazane")) return;
      for( int i=1; i<crystal_states.size; i++)	{ 
        if(!(crystal_states.get(i) == "red"))
        { 
          return;
        }
    }
     
    for( int i=0; i<crystal_states_2.size; i++)	{ 
      if( !(crystal_states_2.get(i) == "blue") )
      {
        return;
      }    
    }   
   sgmstate = "animacja_oczu";  
   angrom_normal_l.anfadein(1500,func{angrom_normal_l.anfadeout(1500,"advsetstdstate");});
   angrom_normal_p.anfadein(1500,func{angrom_normal_p.anfadeout(1500,null);});
   fxpodswietl.play;
    clsave.bset("bn_04_glowa_1_kregi_rozwiazane");
  }
  
  open_skull()
  {
    anglowa1_element.anfadein(700,null); 
    angrom_normal_g.anfadein(700,null); 
    angrom_normal_d.anfadein(700,null); 
    anczaszka_l.play("open"); 
    anczaszka_p.play("open");
    fxpodswietl.play;
    clsave.bset("bn_04_glowa_1_czaszka_otwarta");
  } 
    
  USE_krysztal_blue_1	{
    .advsetstdstate();
		.click_on_empty("krysztal_blue_1", "blue");
  }
  
  USE_krysztal_blue_2	{
    .advsetstdstate();
		.click_on_empty("krysztal_blue_2", "blue");
  }
  
  USE_krysztal_red_1	{
    .advsetstdstate();
		.click_on_empty("krysztal_red_1", "red");
  }
  
  USE_krysztal_red_2	{
    .advsetstdstate();
		.click_on_empty("krysztal_red_2", "red");
  }
  
  click_on_empty(string item, string color)  {
      int index = -1;
      for( int i=0; i<15; i++)	{ 
          @name = "ankrysztal_"+i+"_mask";
          if (<name>.isin(mouse.getpos,0,1))
          {
            index = i;
          }
       }     
       
       if (index>-1)
       {
          if  (<"ankrysztal_"+index>.actionnr(-1)== 3 )
            .put_crystal(index,item, color);
          else 
              .item_gotopck;
      }
       else
       {
          .item_gotopck;
       }     
       
  }
  
  put_crystal(int index, string item, string color)
  {
	fxszczeka.play;
    <"ankrysztal_"+index>.play(color);
    .removefrompck(item); 
    if (index < 8)
    {
        crystal_states.set(index,color);
    }
    else
    {
      crystal_states_2.set(index-8,color); 
    }
    .save_ring();  
    .check_rings(); 
  }
  
  save_ring()
  {
     for( int i=0; i<15; i++)	{ 
      if (i < 8)
      { 
          clsave.set("ankrysztal_"+i,crystal_states.get(i)); 
      }
       else
       {
          clsave.set("ankrysztal_"+i,crystal_states_2.get(i-8));
       }
    }
    clsave.bset("mg04_glowa1_saved");     
  }
  load_ring()
  {
      for( int i=0; i<8; i++)	{ 
          crystal_states.set(i,clsave.get("ankrysztal_"+i));
      }
      crystal_states_2.set(0,clsave.get("ankrysztal_0"));
      for( int i=8; i<15; i++)	{ 
        crystal_states_2.set(i-8,clsave.get("ankrysztal_"+i));
      }   
      .update_crystals(); 
  }
  
  find_empty_slot()
  {
      int index = -1;
      for( int i=0; i<15; i++)	{ 
          if  (<"ankrysztal_"+i>.actionnr(-1)== 3 )
          {
            return i;
          }
       }     
      return -1; 
  }   
    
  mend	{
    
    .stdexitdefadv("ad04_brzegjeziora", "gamead04_brzegjeziora0");
	}   
 
  anglowa1_element_TAKE {
    butgame.onmousemove;
    .popitem_gotocenter("glowa1_element");
    .set_mg_finished;  
  }
       
  anglowa1_element_INPCK {
    .addtopck("glowa1_element"); 
    angrom_normal_g.anfadeout(700,null);
    angrom_normal_d.anfadeout(700,null);
    anczaszka_l.play("close"); 
    anczaszka_p.play("close"); 
    fxpodswietl.play;
    .timfinito;
  } 

  
  krysztal_in_inventory{
    if (clsave.bis("ma_krysztal_blue_1"))
      return true;
    else if (clsave.bis("ma_krysztal_blue_2"))
      return true;
    else if (clsave.bis("ma_krysztal_red_1"))
      return true;
    else if (clsave.bis("ma_krysztal_red_2"))
      return true;
    else return false;
  }
  
  HINT_CLICK {
      int empty_slot = .find_empty_slot();
      if (empty_slot > -1 && .krysztal_in_inventory)
      {
        .showhint("ankrysztal_"+empty_slot+"_mask");
         if (clsave.bis("ma_krysztal_blue_1"))
          .showhintitem("krysztal_blue_1");
        else if (clsave.bis("ma_krysztal_blue_2"))
          .showhintitem("krysztal_blue_2");
        else if (clsave.bis("ma_krysztal_red_1"))
          .showhintitem("krysztal_red_1");
        else if (clsave.bis("ma_krysztal_red_2"))
          .showhintitem("krysztal_red_2");
      }
      else if (.find_empty_slot < 0) 
      {
        bool shown = false;
        if (!clsave.bis("bn_04_glowa_1_lewe_oko_zamkniete"))
        { 
            .showhint("anoko_l");
            shown = true;
        }
        if (!clsave.bis("bn_04_glowa_1_prawe_oko_zamkniete"))
        { 
            .showhint("anoko_p");
            shown = true;
        }
        
        if (clsave.bis("bn_04_glowa_1_lewe_oko_zamkniete")&& clsave.bis("bn_04_glowa_1_prawe_oko_zamkniete") && !clsave.bis("taken_glowa1_element"))
        {
            .showhint("anglowa1_element");
            shown =  true;  
        }
        if (!shown)
        {
          .showhint("anpopexit");       
        }
      }
      else
        .showhint("anpopexit");   
        
    } 
   
}
 
