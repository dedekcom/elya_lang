
game mgBook : Banshee	{
	init	{
		Banshee::init("sys_mgbook");
		
		.imgs = (A, "bkg.jpg", "nextp.pyzH 50", "prevp.pyzH 50", "nakladki.pyzG 20", "zak.pyzGH 10", "pageleft.pyzH", "pageright.pyzH",
			"popexit.pyz 150", "headerl.pyzH", "headerp.pyzH", "posp.pyzH", "posl.pyzH",
			"ptakl.pyzH 10", "ptakp.pyzH", "skresll.pyzH 10", "skreslp.pyzH 10", "txtobj.pyzH",
			"n004_monety.pyzGH 15", "n020a_znaki.pyzGH 16");
		anpageleft.addtogamevars("anpagel");
		anpageright.addtogamevars("anpagep");
		.butpopexit;
		
		.sfxs = (A, "book2", "book3", "medalionleft");
		mus.playstr("libstart2.ogg -v 50 libloop2.ogg -v 50 -l");
		
		new db dbl;
		dbl.load( LANGDIR + "sys_book_story.db" );
		new db dbm;
		dbm.load( LANGDIR + "sys_magic_story.db" );
		
		string s0 = dbconf.dbget("hoguifont:");
		if( s0==null ) s0 = sgmfontitalic;
		//string s0 = "configs/fonts/havlova.ttf";
		
		string sfntfold = dbconf.dbget("hoguifont:");
		if( sfntfold==null ) sfntfold = sgmfontitalic;
		
		.newfont("fntnote", s0, 20->igetsc);
		.newfont("fntitle", s0, 28->igetsc);
		.newfont("fntfold", sfntfold, 17->igetsc);
		.newtext("txtfold", "", "fntfold", .black);
		.newtext("txtpage", "", "fntnote", .black);
		.newtext("txtitl", "", "fntitle", .black);
		.newtext("txtitp", "", "fntitle", .black);
		.newtext("txtpnl", "", "fntitle", .black);
		.newtext("txtpnp", "", "fntitle", .black);
		new anima anpl;		new anima anpp;
		new img imgtxtl;	new img imgtxtp;
		new anima antxt;	
		new gmimgvec grnotel;	new gmimgvec grnotep;
		grnotel.addlist(A, "anpl", "imgtxtl", "txtitl", "txtpnl");
		grnotep.addlist(A, "anpp", "imgtxtp", "txtitp", "txtpnp");
		new gmimgvec grnote;
		grnote.addlist(A, "grnotel", "grnotep", "grn004_monety");
		new int ioffsetp = anposp.getpx - anposl.getpx;
		new gmimgvec grznaki;
		grznaki.addlist(A, "grn020a_znaki");
		 
		new img imgobj;
		new gmimgvec grobj;
		grobj.add("imgobj");
		new vector vobj;
		
		new img imgmag;
		new img imgmagpic;
		new gmimgvec grmag;
		grmag.add("imgmag");
		new int iilemag;
		for( int i=0; i<16; i++ )	new img <"imgmagznak"+i>;
		
		string s = grzak.getsac("note");
		real rdx = 0.35;
		real rdy = 0.35;
		.mprintfold( dbgame.findbyrow("book_notes"), <s>.getpx + rdx*<s>.getw, <s>.getpy + rdy*<s>.geth );
		s = grzak.getsac("objectives");
		.mprintfold( dbgame.findbyrow("book_objectives"), <s>.getpx + rdx*<s>.getw, <s>.getpy + 0.25*<s>.geth );
		s = grzak.getsac("magic");
		if( !clsave.has( "sysbook_magicile_pages") )	{
			txtfold.createtxt(.grey(64));
		}
		.mprintfold( dbgame.findbyrow("book_magic"), <s>.getex - rdx*<s>.getw - fntfold.getsize/2, <s>.getpy + rdy*<s>.geth );
		txtfold.createtxt(.black);
		txtfold.txtset("");
		
		@sfun = func {
			.Button_isinvisalpha;
			.addmethod("butmoveon", func {
				.anfadein(200,null);
				advmouse.setstd;
				} );
			.addmethod("butmoveoff", func {
				.anfadeout(200,null);
				advmouse.setstd;
				} );
			butgame.add(this);
		};
		annextp.<sfun>;
		annextp.addmethod("butclick", func {
			if( .advstdstate )	{
				<GAME>.nextpage(2);
			}
			} );
		anprevp.<sfun>;
		anprevp.addmethod("butclick", func {
			if( .advstdstate )	{
				<GAME>.nextpage(-2);
			}
			} );
		
		new string sactivepage = null;
		grzak.eval( func {
			.vars2(A, "bactive", false );
			.Button_isinvis;
			.addmethod("butmoveon", func {
				.show;
				advmouse.setstd;
				} );
			.addmethod("butmoveoff", func {
				if( !bactive )
					.hide;
				advmouse.setstd;
				} );
			.addmethod("butclick", func {
				if( .advstdstate ) {
					@s = .actionname;
					if( s=="magic" && !clsave.has( "sysbook_magicile_pages") )	{
					} else	{
						if (s!= .getactfold)
							fxbook3.play;
						<GAME>.show_zakladka(s);
					}
				}
				} );
			butgame.add(this);
			} );
		
		.show_actfold;
		
		ccs.enter( func {
			/*if( !clsave.bis("guibook_taken") )	{
				clsave.bset("guibook_taken");
				if( .enabled_tutorial )	{
					.show_tutorial("T5iconbook", .tutpos);
					.tutpoint("anguibook", "ld");
					anguibook.anfadein(500,null);
				}
			}*/
			
			.advsetstdstate;
			} );
	}
	restore_imgobj	{
		imgobj.create( igmappw, igmapph, .transparent );
		imgobj.setz(30);
	}
	mprintfold(int irow, int x, int y)	{
		for( int i=1; i< dbgame.getcolsno(irow); i++) {
			txtfold.txtset(dbgame.get(irow,i));
			txtfold.setpos(x,y);
			imgbkg.blit("txtfold");
			y+=txtfold.geth;
		}
	}
	show_actfold	{	.show_zakladka( .getactfold );	}
	getactfold	{	clsave.get("sysbook_actfold");	}
	show_zakladka(string s)	{
		if( s=="magic" && !clsave.has( "sysbook_magicile_pages") )
			return;
		if( s == null ) s = "note";
		clsave.set("sysbook_actfold", s);
		
		if( sactivepage!=null )	{
			<sactivepage>.bactive = (false);
			<sactivepage>.hide;
		}
		string san = grzak.getsac(s);
		sactivepage = san;
		<san>.bactive = (true);
		grnakladki.show;
		.hidenak(s);
		
		.hide_folds;
		
		string s2 = clsave.get("sysbook_show_"+s);
		if( s2==null )	.show_cur_page(s);
		else {
			.show_obj_page(s, s2);
			clsave.set("sysbook_show_"+s, null);
		}
	}
	hide_folds	{
		grnote.hide;
		grobj.hide;
		grmag.hide;
		grn020a_znaki.hide;
	}
	hidenak(string s)	{	<grnakladki.getsac(s)>.hide;	}
	mend	{
		mus.fadeout(1000);
		.stdexitgame;
	}
	show_cur_page(string s)	{
		int id = clsave.get("sysbook_"+s+"_curpage");
		if( id == 0 )	{
			id = clsave.get("sysbook_"+s+"ile_pages");
			if( id==0 ) return;
			clsave.set("sysbook_"+s+"_curpage", id);
		}
		.show_page(s,id);
	}
	show_page(string s, int p)	{
		if( p < 0 ) p = 1;
		int pl, int pr;
		if( p%2 )	{
			pl = p;
			pr = p+1;
		} else {
			pl = p-1;
			pr = p;
		}
		if(s=="objectives" ) .restore_imgobj;
		if(s=="magic")	.show_magicpage(pl);
		else {
			grn004_monety.hide;
			grn020a_znaki.hide;
			grn004_monety.setpos(0,0);
			grn020a_znaki.setpos(0,0);
			.<"show_"+s+"dir">( "l", pl );
			.<"show_"+s+"dir">( "p", pr );
		}
	}
	enable_note(string s)	{
		int page = clsave.get( "sysbook_note_"+s );
		if( page==0 )	{
			page = 1 + clsave.get("sysbook_noteile_pages");
			clsave.set( "sysbook_noteile_pages", page );
			clsave.set( "sysbook_note_"+s, page );
			clsave.set( "sysbook_notepage_"+page, s );
		}
		clsave.set( "sysbook_note_curpage", page );
	}
	enable_objectives(string s)	{
		int page = clsave.get( "sysbook_objectives_"+s );
		if( page==0 )	{
			int isp = clsave.get("sysbook_objectives_lastsubpage");
			if( isp==0 || isp==4 )	{
				page = 1 + clsave.get("sysbook_objectivesile_pages");
				isp = 1;
			} else {
				page = clsave.get( "sysbook_objectives_curpage" );
				isp++;
			}
			clsave.set( "sysbook_objectives_lastsubpage", isp);
			clsave.set( "sysbook_objectives_"+s, page );
			clsave.set( "sysbook_objectives_subpagesinpage_"+page, isp );
			clsave.set( "sysbook_objectivespage_"+isp+"_"+page, s );
			clsave.set( "sysbook_objectivesile_pages", page );
		}
		clsave.set( "sysbook_objectives_curpage", page );
	}
	enable_magic(string s)	{
		int page = clsave.get( "sysbook_magic_"+s );
		if( page==0 )	{
			int ile = clsave.get("sysbook_magicile_pages");
			if( ile==0 ) page = 1;
			else page = ile + 2;
			clsave.set( "sysbook_magicile_pages", page );
			clsave.set( "sysbook_magic_"+s, page );
			clsave.set( "sysbook_magicpage_"+page, s );
		}
		clsave.set( "sysbook_magic_curpage", page );
	}
	show_notedir(string sdir, int page)	{
		string sid = clsave.get("sysbook_notepage_"+page);
		int id = dbl.findbyrow(sid);
		if( id<0 ) {
			<"grnote"+sdir>.hide;
			return;
		}
		
		string s = "txtit"+sdir;
		<s>.txtset( dbl.get(id,1) );
		<s>.setpos( <"anheader"+sdir>.getcx - <s>.getw/2, <"anheader"+sdir>.getcy - <s>.geth/2 );
		
		s = "anp"+sdir;
		<s>.pathload( sid + "_gfx.pyz" );
		
		antxt.pathload( sid + "_txt.pyz" );
		antxt.hide;
		s = "imgtxt" + sdir;
		<s>.imgfittxt( dbl.get(id, 2), "antxt", "txtpage", fntnote.getsize + 4, true );
		<s>.setz( 5 );
		txtpage.hide;
		
		.print_page(page, sdir);
		
		<"grnote"+sdir>.show;
		if( sdir == "p" ) {
			anpp.move( ioffsetp, 0 );
			imgtxtp.move( ioffsetp, 0 );
		}
		
		if( sid == "n004" )	{
			grn004_monety.eval( func {
				if( clsave.bis("takenan_anmoneta"+.actionname) )
					.show;
				} );
			if( sdir=="p" )
				grn004_monety.setpos( ioffsetp, 0 );
		}
		 
		 if( sid == "n020a") {
			grn020a_znaki.hide;
			grn020a_znaki.eval( func {
				if (.actionname == "woda" &&  clsave.bis("e1_mg22_palenisko_woda_dodana"))
					.show;
				else if (.actionname == "fluid" &&  clsave.bis("e1_mg22_palenisko_fluid_dodany"))
					.show;
				else if (.actionname == "ziarna" &&  clsave.bis("e1_mg22_palenisko_ziarna_dodane"))
					.show;	
				else if (.actionname == "frog_leg" &&  clsave.bis("e1_mg22_palenisko_frog_leg_dodana"))
					.show;	
				else if (.actionname == "spell" &&  clsave.bis("e1_mg22_palenisko_mikstura_zrobiona"))
					.show;	
				else if (.actionname == "arat" &&  clsave.bis("e1_mg25_lustro_finished"))
					.show;	
				else if (.actionname == "ron" &&  clsave.bis("e1_mg28_luneta_finished"))
					.show;	
				
			});
			if( sdir=="p" )
				grn020a_znaki.setpos( ioffsetp, 0 );
			 
		}
		
	}
	
	show_objectivesdir(string sdir, int page)	{
		int isp = clsave.get( "sysbook_objectives_subpagesinpage_"+page );
		if( isp==0 ) return;
		
		for( int i=1; i<=isp; i++ )	{
			string sid = clsave.get("sysbook_objectivespage_"+i+"_"+page);
			int id = dbobject.findbyrow(sid);
			if( id>=0 ) {
				antxtobj.setframe( sdir + i, 0 );
				imgobj.imgfittxt( dbobject.dbrowtostring(id, 1, -1, " "), "antxtobj", "txtitl", fntnote.getsize + 3, false );
				if( clsave.bis("objectiveid_"+sid) )	{
					anptakl.ansetbpos( antxtobj.getex, antxtobj.getcy - anptakl.geth/2 );
					imgobj.blit("anptakl");
				}
			}
		}
		.print_page(page, sdir);
		imgobj.blit("txtpn"+sdir);
		imgobj.show;
	}
	show_magicpage(int page)	{
		
		if(clsave.bis("sysbook_to_show"))
		{
			//clsave.set("sysbook_to_show", false); 
			page  = clsave.get("sysbook_to_show_page");
			anguibookmagic.play("magic");
			anguibookmagic.hide;
		}	
		 
		 
		string sid = clsave.get("sysbook_magicpage_"+page);
		int id = dbm.findbyrow(sid);
		if( id<0 ) {
			grmag.hide;
			return;
		}
		iilemag = 0;
		.newanima("anmagbg", sid + "_bkg.pyz", 0);
		imgmag.create( igmappw, igmapph, .transparent );
		imgmag.blit("anmagbg");
		delete anmagbg;
		
		int znadpage = 24;
		
		.newanima("anmagznak", sid + "_znaki.pyz", 0);
		bool ballznak = true;
		for( int i=0; i<anmagznak.nofactions; i++)	{
			anmagznak.setframe(i,0);
			if( clsave.bis("e1_an"+anmagznak.actionname+"_wziety") )	{
				if( clsave.bgo("e1_an"+anmagznak.actionname+"_odsloniety") )	{
					ballznak = false;
					string s = "imgmagznak"+iilemag;
					iilemag++;
					<s>.clone("anmagznak");
					<s>.setz(znadpage);
					sgmstate = "state_enablekulfon";
					fxmedalionleft.playif;
					<s>.anfadein( 1000, func {
						if(!clsave.bis("sysbook_to_show"))
							.hide;
						iilemag--;
						if( iilemag<=0 )	{
							if(!clsave.bis("sysbook_to_show"))
							{
								.advsetstdstate;
								<GAME>.show_actfold;
							} 
						}
						} );
				} else
					imgmag.blit("anmagznak");
			} else ballznak=false;
		}
		delete anmagznak;
		
		if( ballznak || clsave.bis("sysbook_to_show"))	{
			clsave.set("sysbook_to_show", false); 
			string s;
			bool bcomplete = clsave.bgo("sysbook_magic_page_"+page+"_complete");
			.newanima("anmagstory", sid + "_story.pyz", 0);
			if( bcomplete )	{
				s = "imgmagpic";
				@w = igmappw/2;
				imgmagpic.create(w, igmapph, .transparent);
				imgmagpic.setpos(w,0);
				imgmagpic.setz( znadpage );
			} else {
				s = "imgmag";
			}
			anmagstory.setframe("title",0);
			txtitl.txtset(dbm.get(id,1));
			txtitl.txtcenter("anmagstory");
			<s>.blit("txtitl");
			anmagstory.setframe("txt",0);
			<s>.imgfittxt( dbm.get(id,2), "anmagstory", "txtpage", fntnote.getsize + 3, false );
			delete anmagstory;
			txtitl.hide;
			txtpage.hide;
			if( bcomplete )	{
				sgmstate = "state_enablemagicpage";
				fxmedalionleft.play;
				/*
				 imgmagpic.anfadein(1000, func { 
					imgmagpic.hide;
					.advsetstdstate;
					<GAME>.show_actfold;
				} ); */
				 imgmagpic.anfadein(1000,null);
				imgmagpic.hide;
				<GAME>.show_actfold;
				.advsetstdstate; 
			}
			 
		}
		
		.print_page(page, "l");
		.print_page(page+1, "p");
		imgmag.blit("txtpnl");
		imgmag.blit("txtpnp");
		imgmag.show;
		imgmag.setz(22);
		
		if( .enabled_tutorial )	{
			if (!clsave.bis("e1_T17magic_shown"))
			{
				.show_tutorial("T17magic", .tutpos);
				clsave.bset("e1_T17magic_shown"); 
			}
		} 
		
	}
	show_obj_page(string sob, string s)	{
		.<"enable_"+sob>(s);
		.show_page(sob, clsave.get("sysbook_"+sob+"_"+s) );
	}
	print_page(int page, string sdir)	{
		string s = "txtpn"+sdir;
		<s>.txtset(page);
		<s>.txtcenter( "anpage"+sdir );
	}
	int _nextpage(string s, int page)	{
		page = page + clsave.get("sysbook_"+s+"_curpage");
		if( page < 1 ) 
			page = 1;
		else if ( page > clsave.get("sysbook_"+s+"ile_pages")->to_i )
			page = clsave.get("sysbook_"+s+"ile_pages");
		else
			fxbook2.play;
		clsave.set("sysbook_"+s+"_curpage", page);
		page;
	}
	nextpage(int id)	{
		@s = .getactfold;
		int ile = clsave.get("sysbook_"+s+"ile_pages");
		if( ile < 3 ) return;
		 
		.show_page(s, ._nextpage(s,id) );
	}
}
