
game mg01Schody : Banshee	{
	init	{
		Banshee::init("mg01_schody");
		
		.imgs = (A, "bkg.jpg", "jezyk.pyzP 5", "jaszczur.pyz 10",
			"przycisk.pyz 20", "klucz.pyz 25",
			"dolcien.pyz 30", "klapa.pyz 35", "klapamask.pyz 40",
			"dolekputmedal.pyz 45", "medalput.pyzH 50", "medalpck.pyzH 55",
			"lisc1.pyz 60", "lisc2.pyz 60", "lisc3.pyz 60", "lisc4.pyz 60",
			"cienmask.pyz 70", "motyl.pyzP 115", "ramka.pyzG 80", "popexit.pyz 90");
		.butpopexit;
		
		.setasitemtopck("anklucz");
		if( !clsave.bis("e1_mg01_klucz_odkryty") )
			anklucz.disable = (true);
		else anklapa.hide;
		
		anmotyl.ansetbpos( igmappw/2+250,igmapph/2);
		new filter ftmotyl;
		ftmotyl.link("anmotyl");
		new int ilicz = 0;
		anmotyl.addmethod("onendframe", func {
			ilicz++;
			if( ilicz==1 )	{
				ilicz=0;
				real kat = 12-21->rand;
				real dt = 5;
				real kat2 = ftmotyl.getangle;
				real x = kat2->sin * dt;
				real y = kat2->cos * dt;
				ftmotyl.rotate(kat);
				.move(-x,-y);
			}
			} );
		
		if( !clsave.bis("e1_zabrana_czaszka3") )	{
			.imgs = (A, "czaszka3.pyz 75");
			.setasitemtopck("anczaszka3");
		}
		for( int i=1; i<=4; i++)	{
			if( clsave.bis("e1_mg01_lisc_"+i+"_odsuniety") ) {
				<"anlisc"+i>.ansetlastframe(1);
			} else {
				<"anlisc"+i> {
					.Button_isinalpha;
					.addmethod("butmoveon", func {
						if( sgmstate!="play" ) return;
						advmouse.setact;
						} );
					.addmethod("butmoveoff", func {
						if( sgmstate!="play" ) return;
						advmouse.setstd;
						} );
					.addmethod("butclick", func {
						if( sgmstate!="play" ) return;
						if( .actionnr(-1)==0 )	{
							butgame.removebut(this);
							butgame.onmousemove;
							clsave.bset("e1_mg01_lisc_"+ this->gete(0,1) +"_odsuniety");
							.play(1);
						}
						} );
					butgame.add(this);
				};
			}
		}
		andolekputmedal {
			.addmethod("isbutin", func { (@x, @y)
				anlisc1.actionnr(-1)==1 && .isin(x,y,0,0);
				} );
			.addmethod("butmoveon", func {
				if( sgmstate!="play" && sgmstate!="getitem" ) return;
				advmouse.set("take");
				} );
			.addmethod("butmoveoff", func {
				if( sgmstate!="play" && sgmstate!="getitem" ) return;
				advmouse.setstd;
				} );
			butgame.add(this);
		};
		if( clsave.bis("e1_medalfontanna_is_put") )	{
			anmedalput.show;
		} 
		if( !clsave.bis("e1_mg01_klucz_odkryty") )	{
			anprzycisk	{
				.addmethod("isbutin", func { (@x, @y)
					.przyciskodkryty && .isin(x,y,0,0);
					} );
				.addmethod("butmoveon", func {
					if( sgmstate!="play" ) return;
					advmouse.set("take");
					} );
				.addmethod("butmoveoff", func {
					if( sgmstate!="play" ) return;
					advmouse.setstd;
					} );
				.addmethod("butclick", func {
					if( sgmstate!="play" ) return;
					if( .actionnr(-1) ) .setframe(0,0);
					else .setframe(1,0);
					if( anmedalput.isvisible && .actionnr(-1) )	{
						butgame.removebut(this);
						butgame.onmousemove;
						sgmstate = "odsun";
						timzasuwa.play;
					}
					} );
				butgame.add(this);
			};
		}
		.cycle = ("timzasuwa", func {
			anklapa.move(0, -10);
			if( anklapa.getpy < 0 )	{
				sgmstate = "play";
				anklucz.disable=(false);
				clsave.bset("e1_mg01_klucz_odkryty");
			} else .play;
			} );
		
		.stdstart;
	}
	anklucz_TAKE	{	.item_gotocenter;	}
	anklucz_INPCK	{
		.addtopck("kluczschodek");
		clsave.bset("e1_end_schody");
	}
	mend	{
		/*sgmstate="end";
		.timplay("timend", 500, func {
			.gotoadv("ad01_widokogolny", "gamead01_widokogolny0");
			ccs.buildfromscreen;
			} );*/
		.stdexitdefadv("ad01_widokogolny", "gamead01_widokogolny0");
	}
	przyciskodkryty	{	anlisc2.actionnr(-1) && anlisc3.actionnr(-1) && anlisc4.actionnr(-1);	}
	anczaszka3_INPCK	{
		.addtopck("czaszka3");
		clsave.bset("e1_zabrana_czaszka3");
	}
	USE_medalfontanna	{
		.advsetstdstate;
		if( andolekputmedal.isbutin(mouse.getpos) )	{
			butgame.removebut("andolekputmedal");
			.removefrompck("medalfontanna");
			anmedalput.show;
			clsave.bset("e1_medalfontanna_is_put");
		} else .item_gotopck;
	}
	HINT_CLICK	{
		if( !clsave.bis("e1_wyciagnela_kolatke") )	{
			.showhint("anpopexit");
		} else {
			if( !clsave.bis("e1_medalfontanna_taken") )	{
				if( clsave.bis("e1_zabrana_czaszka3") )
					.showhint("anpopexit");
				else .showhint("anczaszka3");
			} else if( !clsave.bis("e1_mg01_lisc_1_odsuniety") )	{
				.showhint("anlisc1");
			} else if( !clsave.bis("e1_mg01_lisc_2_odsuniety") )	{
				.showhint("anlisc2");
			} else if( !clsave.bis("e1_mg01_lisc_3_odsuniety") )	{
				.showhint("anlisc3");
			} else if( !clsave.bis("e1_mg01_lisc_4_odsuniety") )	{
				.showhint("anlisc4");
			} else if ( !clsave.bis("e1_medalfontanna_is_put") )	{
				.showhint("andolekputmedal");
				.showhintitem("medalfontanna");
			} else if(!clsave.bis("e1_mg01_klucz_odkryty"))	{
				.showhint("anprzycisk");
			} else if ( !clsave.bis("e1_end_schody") )	{
				.showhint("anklucz");
			} else .showhint("anpopexit");
		}
	}
}

