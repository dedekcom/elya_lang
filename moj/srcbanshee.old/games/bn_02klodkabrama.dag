
game mg02Klodkabrama : Banshee	{
	init	{
		Banshee::init("mg02_klodkabrama");
		.imgs = (A, "bkg.jpg", "lew.pyz 30", "oczy.pyzH 40",
			"box.pyzH 5", "els.pyzGH 10", "forklucz.pyzH 15", "kolatka.pyzH 40", "klucz.pyzH 20",
			"popexit.pyz 50");
		
		.butpopexit;
		
		if( !clsave.bis("e1_lew_losuj") )	{
			clsave.bset("e1_lew_losuj");
			for( int i=0; i<grels.size; i++)	{
				@s = grels.get(i);
				<s>.setframe(i, 1 + (<s>.nofframes(-1)-1)->rand );
				clsave.set("e1_lew_els_"+i, <s>.framenr );
			}
		} else {
			for( int i=0; i<grels.size; i++)	{
				<grels.get(i)>.setframe( i , clsave.get("e1_lew_els_"+i) );
			}
		}
		
		.sfxs = (A, "odklucza2");
		new bool bkoniecmech;
		.mcheckels;
		
		anklucz.addmethod("onfinish", func {
			clsave.bset("e1_lew_otworzony");
			.mend;
			} );
		ankolatka {
			.Button_isin;
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate ) return;
				sgmstate = "lewopen";
				.play(0);
				} );
			.addmethod("onfinish", func {
				.hide;
				clsave.bset("e1_lew_mech");
				.mlewmech;
				anlew.playsmooth("open");
				} );
			butgame.add(this);
		};
		anlew {
			.addmethod("isbutin", func { (int x, int y)
				.isin(x,y,1,1) && (.actionname=="normal" || (.actionname=="sleep" && !ankolatka.isvisible));
				} );
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate ) return;
				if( .actionname=="normal" )	{
					sgmstate = "angrylew";
					.playsmooth("angry");
					anoczy.play("angry");
				}
				} );
			.addmethod("onfinish", func {
				if (.actionname=="angry")	{
					.play("angry2");
				} else if (.actionname=="angry2")	{
					.playsmooth("angry3");
				} else if (.actionname=="angry3")	{
					.advsetstdstate;
					.play("normal");
					anoczy.play("normal");
				} else if ( .actionname=="open" )	{
					.advsetstdstate;
					.setframe("opened", 0);
				} else if (.actionname=="sleep")	{
					.advsetstdstate;
				}
				} );
			butgame.add(this);
		};
		grels.eval( func {
			.addmethod("isbutin", func { (int x, int y)
				!bkoniecmech && .isin(x,y,1,1);
				} );
			.Button_moveonms;
			.Button_moveoffms;
			.addmethod("butclick", func {
				if( !.advstdstate || bkoniecmech ) return;
				.ansetnextfr;
				clsave.set("e1_lew_els_" + .actionnr(-1), .framenr);
				.mcheckels;
				if( bkoniecmech )	{
					fxodklucza2.play;
				}
				} );
			butgame.add(this);
			} );
		
		anforklucz	{
			.addmethod("isbutin", func { (int x, int y)
				bkoniecmech && .isin(x,y,1,0);
				} );
			.Button_moveonms;
			.Button_moveoffms;
			butgame.add(this);
		};
		
		//lew: normal angry sleep open opened
		if( clsave.bis("e1_lew_mech") )	{
			anlew.setframe("opened",0);
			.mlewmech;
		} else if ( clsave.bis("e1_lew_kolatka") )	{
			anlew.setframe("sleep", 0);
			ankolatka.show;
		} else {
			anoczy.play(0);
		}
		.stdstart;
		/*if( clsave.bis("e1_lew_mech") )	{
			.stdstart;
		} else
			.advsetstdstate;*/
	}
	mlewmech	{
		anbox.show;
		grels.show;
		anforklucz.show;
	}
	mcheckels	{
		for( int i=0 ; i<grels.size; i++) {
			if( !<grels.get(i)>.framename->contains("ok") ) {
				bkoniecmech = false;
				return;
			}
		}
		bkoniecmech = true;
	}
	USE_kolatka	{
		if( anlew.isin(mouse.getpos,1,1) )	{
			.removefrompck("kolatka");
			anlew.play("sleep");
			anoczy.anhide;
			ankolatka.show;
			clsave.bset("e1_lew_kolatka");
		} else {
			.advsetstdstate;
			.item_gotopck;
		}
	}
	USE_kluczschodek	{
		if( bkoniecmech && anforklucz.isin(mouse.getpos,1,0) )	{
			.removefrompck("kluczschodek");
			sgmstate = "kluczotwiera";
			anklucz.play(0);
		} else {
			.advsetstdstate;
			.item_gotopck;
		}
	}
	mend	{
		.stdexitdefadv("ad02_brama", "gamead02_brama0");
	}
}


